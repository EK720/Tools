cmake_minimum_required(VERSION 3.16)
project(easyrpg-tools VERSION 1.1 LANGUAGES CXX)

option(DISABLE_LMU2PNG "Disable lmu2png converter tool" OFF)
option(DISABLE_PNG2XYZ "Disable png2xyz converter tool" OFF)
option(DISABLE_XYZ2PNG "Disable xyz2png converter tool" OFF)
option(DISABLE_GENCACHE "Disable gencache tool" OFF)
option(DISABLE_XYZCRUSH "Disable xyzcrush tool" OFF)
option(DISABLE_LCFTRANS "Disable lcftrans tool" OFF)
option(DISABLE_LCFVIZ "Disable lcfviz tool" OFF)
if(WIN32)
	option(DISABLE_XYZTHUMBNAILER "Disable xyz-thumbnailer plugin" OFF)
endif()

set(CMAKE_MODULE_PATH APPEND "${CMAKE_CURRENT_SOURCE_DIR}/Modules")

function(enable_tool tool)
	string(TOUPPER ${tool} tool_upper)
	if(NOT DISABLE_${tool_upper})
		add_subdirectory(${tool})
	endif()
endfunction()

macro(tool_is_enabled tool)
	string(TOUPPER ${tool} tool_upper)
	if(NOT DISABLE_${tool_upper})
		set(TOOL_ENABLED "yes")
	else()
		set(TOOL_ENABLED "no")
	endif()
	mark_as_advanced(TOOL_ENABLED)
	mark_as_advanced(tool_upper)
endmacro()

foreach(tool lmu2png png2xyz xyz2png gencache xyzcrush lcftrans lcfviz)
	enable_tool(${tool})
endforeach()
if(WIN32 AND NOT DISABLE_XYZTHUMBNAILER)
	add_subdirectory(xyz-thumbnailer/windows)
endif()

message(STATUS "")
message(STATUS "Summary:")
message(STATUS "========")
message(STATUS "")
message(STATUS "  prefix:  ${CMAKE_INSTALL_PREFIX}")
message(STATUS "")
message(STATUS "Converters:")
foreach(tool lmu2png png2xyz xyz2png)
	tool_is_enabled(${tool})
	message(STATUS "  ${tool}:  ${TOOL_ENABLED}")
endforeach()
message(STATUS "")
message(STATUS "Other:")
foreach(tool xyzcrush gencache lcftrans lcfviz)
	tool_is_enabled(${tool})
	message(STATUS "  ${tool}:  ${TOOL_ENABLED}")
endforeach()
message(STATUS "")
